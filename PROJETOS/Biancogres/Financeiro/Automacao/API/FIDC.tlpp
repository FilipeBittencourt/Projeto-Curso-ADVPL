#include "totvs.ch"
#include "parmtype.ch"
#include "dbstruct.ch"

/*/{Protheus.doc} FIDC
@author Marinaldo de Jesus
@since 01/02/2021
@project FIDC
@version 1.0
@description Classe para Tratamento FIDC
@type class
/*/

static aBCOIsFIDC   as array
static aRGRIsFIDC   as array

static oFIDCVars    as object
static oFIDCPars 	as object

class FIDC

	static method isFIDCEnabled() as logical

	static method bcoIsFIDC() as logical
	static method getbcoFIDC(lClear as logical) as array

	static method retbcoFIDC() as array

	static method regrabcoIsFIDC() as logical
	static method getregrabcoIsFIDC(lClear as logical) as array

	static method ctbFIDC() as numeric
	static method isctbFIDC() as logical
	static method isLPctbFIDC(cPadrao as character) as logical
	
	static method ctbFIDCOffLine() as logical
	static method txFIDCOffLine() as logical

    static method setFIDCVar(uVar,uVal)
    static method getFIDCVar(uVar,uDefault)
    static method resetFIDCVars()

	static method percentualDesconto() as valor
	static method calculaDesconto(nValor as numeric,dVencimento as date,dReferencia as date) as valor

	static method getBiaPar(cParam as character,xDefault,cEmp as character,cFil as character)
	static method putBiaPar(cParam as character,cConteudo as character,cEmp as character,cFil as character)
	static method existBiaPar(cParam as character,cEmp as character,cFil as character) as logical
	static method InsertBiaPar(cParam as character,cConteudo as character,cEmp as character,cFil as character) as logical
	static method resetFIDCPars()

	static method getQueryRem(aParamRet as array,aBCOIsFIDC as array,aFWTTables as array) as character
	static method getQueryRet(aParamRet as array,aBCOIsFIDC as array,aFWTTables as array) as character
	static method getQueryRec(aParamRet as array,aBCOIsFIDC as array,aFWTTables as array) as character
	static method getQueryCon(aParamRet as array,aBCOIsFIDC as array,aFWTTables as array) as character

end class

static method isFIDCEnabled() class FIDC

	static s__cKeyFIDC		as character
	static s__lA6YTPINTB	as logical

	local lChkFiDC			as logical
	DEFAULT s__cKeyFIDC:="__cKeyFIDC__"

	lChkFiDC:=(type("cEmpAnt")=="C").and.(!&("cEmpAnt")==s__cKeyFIDC)
	if (lChkFiDC)
		s__cKeyFIDC:=&("cEmpAnt")
		s__lA6YTPINTB:=SA6->(FieldPos("A6_YTPINTB")>0)
	endif

	DEFAULT s__lA6YTPINTB:=.F.

	return(s__lA6YTPINTB)

static method BCOIsFIDC() class FIDC

    local aArea    		as array

	local bBCOIsFIDC	as block

	local cEmp			as character
	local cFil			as character

	local cBanco        as character
    local cAgencia      as character
    local cConta        as character
    local cTmpAlias     as character

    local lBCOIsFIDC    as logical

    local nSA6RecNo     as numeric
	local nBCOIsFIDC	as numeric

    cBanco:=FIDC():getFIDCVar("cBanco","")
    cAgencia:=FIDC():getFIDCVar("cAgencia","")
    cConta:=FIDC():getFIDCVar("cConta","")

	cEmp:=&("cEmpAnt")
	cFil:=&("cFilAnt")

	DEFAULT aBCOIsFIDC:=array(0)
	bBCOIsFIDC:={|e|(e[1]==cEmp).and.(e[2]==cFil).and.(e[3]==cBanco).and.(e[4]==cAgencia).and.(e[5]==cConta)}
	nBCOIsFIDC:=aScan(aBCOIsFIDC,bBCOIsFIDC)
	if (nBCOIsFIDC==0)

		aArea:=getArea()

		cTmpAlias:=getNextAlias()

		beginSQL alias cTmpAlias
			SELECT SA6.R_E_C_N_O_ SA6RECNO
				FROM %table:SA6% SA6
			WHERE SA6.%notDel%
				AND SA6.A6_FILIAL=%xFilial:SA6%
				AND SA6.A6_COD=%exp:cBanco%
				AND SA6.A6_AGENCIA=%exp:cAgencia%
				AND SA6.A6_NUMCON=%exp:cConta%
				AND SA6.A6_YTPINTB='1'
		endSQL

		nSA6RecNo:=0

		while (cTmpAlias)->(!eof())
			nSA6RecNo:=(cTmpAlias)->SA6RECNO
			if (nSA6RecNo>0)
				lBCOIsFIDC:=.T.
				FIDC():setFIDCVar("nSA6RecNo",nSA6RecNo)
				SA6->(MsGoTo(nSA6RecNo))
				exit
			endif
			(cTmpAlias)->(dbSkip())
		end while

		(cTmpAlias)->(dbCloseArea())
		dbSelectArea("SA6")

    	restArea(aArea)

		aAdd(aBCOIsFIDC,{cEmp,cFil,cBanco,cAgencia,cConta,nSA6RecNo})
		nBCOIsFIDC:=len(aBCOIsFIDC)

	endif

	nSA6RecNo:=aBCOIsFIDC[nBCOIsFIDC][6]
	lBCOIsFIDC:=(!empty(nSA6RecNo))
	if (lBCOIsFIDC)
		SA6->(MsGoTo(nSA6RecNo))
	endif

    return(lBCOIsFIDC)

static method getbcoFIDC(lClear) class FIDC
	DEFAULT lClear:=.F.
	paramtype lClear as logical optional
	DEFAULT aBCOIsFIDC:=array(0)
	if (lClear)
		aSize(aBCOIsFIDC,0)
	endif
	return(aBCOIsFIDC)

static method retbcoFIDC() class FIDC
	
	local cEmp		as character
	local cFil		as character
	local cBanco	as character
	local cAgencia	as character
	local cConta	as character
	local cTmpAlias	as character

	local nSA6RecNo	as numeric

	cTmpAlias:=getNextAlias()

	beginSQL alias cTmpAlias
		SELECT SA6.R_E_C_N_O_ SA6RECNO
		  FROM %table:SA6% SA6
		 WHERE SA6.%notDel%
		   AND SA6.A6_FILIAL=%xFilial:SA6%
		   AND SA6.A6_YTPINTB='1'
	endSQL

	DEFAULT aBCOIsFIDC:=array(0)

	cEmp:=&("cEmpAnt")
	cFil:=&("cFilAnt")

	DEFAULT aBCOIsFIDC:=array(0)
	bBCOIsFIDC:={|e|(e[1]==cEmp).and.(e[2]==cFil).and.(e[3]==cBanco).and.(e[4]==cAgencia).and.(e[5]==cConta)}

	while (cTmpAlias)->(!eof())
		nSA6RecNo:=(cTmpAlias)->SA6RECNO
		SA6->(dbGoTo(nSA6RecNo))
		cBanco:=SA6->A6_COD
		cAgencia:=SA6->A6_AGENCIA
		cConta:=SA6->A6_NUMCON
		nBCOIsFIDC:=aScan(aBCOIsFIDC,bBCOIsFIDC)
		if (nBCOIsFIDC==0)
			aAdd(aBCOIsFIDC,{cEmp,cFil,cBanco,cAgencia,cConta,nSA6RecNo})
		endif
		(cTmpAlias)->(dbSkip())
	end while

	return(aBCOIsFIDC)

static method regrabcoIsFIDC() class FIDC
	
	local aArea    		as array

	local bBCOIsFIDC	as block
	local bRGRIsFIDC	as block

	local cEmp			as character
	local cFil			as character	

	local cBanco        as character
    local cAgencia      as character
    local cConta        as character

	local cTmpAlias 	as character
	local cA1YCDGREG	as character

	local lRGRIsFIDC	as logical

	local nSA6RecNo     as numeric
	
	local nRGRIsFIDC	as numeric
	local nBCOIsFIDC	as numeric

	cA1YCDGREG:=FIDC():getFIDCVar("A1_YCDGREG","")

	cEmp:=&("cEmpAnt")
	cFil:=&("cFilAnt")

	DEFAULT aRGRIsFIDC:=array(0)
	bRGRIsFIDC:={|e|(e[1]==cEmp).and.(e[2]==cFil).and.(e[3]==cA1YCDGREG)}
	nRGRIsFIDC:=aScan(aRGRIsFIDC,bRGRIsFIDC)
	if (nRGRIsFIDC==0)

		aArea:=getArea()

		cTmpAlias:=getNextAlias()

		beginSQL alias cTmpAlias
			SELECT SA6.R_E_C_N_O_ SA6RECNO
			FROM %table:ZK0% ZK0
			JOIN %table:ZK1% ZK1
			  ON ZK0.ZK0_FILIAL=ZK1.ZK1_FILIAL
			 AND ZK0.ZK0_CODREG=ZK1.ZK1_CODREG
			 AND ZK1.%notDel%
			JOIN %table:SA6% SA6
			  ON SA6.A6_FILIAL=%xFilial:SA6%
			 AND SA6.A6_COD=ZK1.ZK1_BANCO
			 AND SA6.A6_AGENCIA=ZK1.ZK1_AGENCI
			 AND SA6.A6_NUMCON=ZK1.ZK1_CONTA
			 AND SA6.%notDel%
		   WHERE ZK0.ZK0_CODGRU=%exp:cA1YCDGREG%
			 AND ZK0.ZK0_FILIAL=%xFilial:ZK0%
			 AND ZK0.%notDel%
		endSQL

		nSA6RecNo:=(cTmpAlias)->SA6RECNO
		SA6->(MsGoTo(nSA6RecNo))

		cBanco:=SA6->A6_COD
		cAgencia:=SA6->A6_AGENCIA
		cConta:=SA6->A6_NUMCON
		
		lIsFIDC:=(SA6->A6_YTPINTB=='1')

		(cTmpAlias)->(dbCloseArea())
		dbSelectArea("SA6")

    	restArea(aArea)

		aAdd(aRGRIsFIDC,{cEmp,cFil,cA1YCDGREG,lIsFIDC,cBanco,cAgencia,cConta,nSA6RecNo})
		
		bBCOIsFIDC:={|e|(e[1]==cEmp).and.(e[2]==cFil).and.(e[3]==cBanco).and.(e[4]==cAgencia).and.(e[5]==cConta)}
		nBCOIsFIDC:=aScan(aBCOIsFIDC,bBCOIsFIDC)
		if (nBCOIsFIDC==0)
			DEFAULT aBCOIsFIDC:=array(0)
			FIDC():setFIDCVar("nSA6RecNo",nSA6RecNo)
			aAdd(aBCOIsFIDC,{cEmp,cFil,cBanco,cAgencia,cConta,nSA6RecNo})
		endif
		
		nRGRIsFIDC:=len(aRGRIsFIDC)

	endif

	lRGRIsFIDC:=aRGRIsFIDC[nRGRIsFIDC][4]
	nSA6RecNo:=aRGRIsFIDC[nRGRIsFIDC][8]
	SA6->(MsGoTo(nSA6RecNo))

	return(lRGRIsFIDC)

static method getregrabcoIsFIDC(lClear) class FIDC
	DEFAULT lClear:=.F.
	paramtype lClear as logical optional
	DEFAULT aRGRIsFIDC:=array(0)
	if (lClear)
		aSize(aRGRIsFIDC,0)
	endif
	return(aRGRIsFIDC)

static method ctbFIDC() class FIDC

	local aArea		as array
	local aDiario	as array
	local aFlagCTB	as array

    local cLote		as character
	local cPadrao   as character
	local cFunCTB	as character
	local cArquivo	as character

	local lPadrao	as logical
	local lDigita	as logical
	local lDiario	as logical
	local lUsaFlag	as logical

	local nTotal	as numeric
	local nHdlPrv	as numeric

	local nSA1RecNo	as numeric
	local nSA6RecNo as numeric
    local nSE1RecNo	as numeric

	aArea:=getArea()

	begin sequence

        if (!FIDC():BCOIsFIDC())
			break
		endif

		nSA1RecNo:=FIDC():getFIDCVar("nSA1RecNo",0)
		if (!empty(nSA1RecNo))
			SA1->(MsGoTo(nSA1RecNo))
		endif

		nSA6RecNo:=FIDC():getFIDCVar("nSA6RecNo",0)
		if (!empty(nSA6RecNo))
			SA6->(MsGoTo(nSA6RecNo))
		endif

		nSE1RecNo:=FIDC():getFIDCVar("nSE1RecNo",0)
		if (!empty(nSE1RecNo))
			SE1->(MsGoTo(nSE1RecNo))
		endif

		cPadrao:=FIDC():getFIDCVar("cPadrao","")

		lPadrao:=VerPadrao(cPadrao)

		if (!lPadrao)
			break
		endif

		cFunCTB:=FIDC():getBiaPar("FIDC_FUNCTB","CTBFIDC")
		cLote:=FIDC():getBiaPar("FIDC_LOTECTB","008850")

		cArquivo:=""
		nHdlPrv:=HeadProva(cLote,cFunCTB,SubStr(&("cUsuario"),7,6),@cArquivo)

		if (empty(nHdlPrv))
			break
		endif

		if (!(nHdlPrv>0))
			break
		endif

		lUsaFlag:=FIDC():getFIDCVar("lUsaFlag",.F.)
		if (lUsaFlag)
			// Armazena em aFlagCTB para atualizar no modulo Contabil
			aFlagCTB:=array(0)
			aAdd(aFlagCTB,FIDC():getFIDCVar("aFlagCTB",{"","","",0,0,0,0}))
		endif

		lDiario:=FIDC():getFIDCVar("lDiario",.F.)
		if (lDiario)
			aDiario:=array(0)
			aAdd(aDiario,FIDC():getFIDCVar("aDiario",{"",0,0,"",""}))
		EndIf

		nTotal:=DetProva(nHdlPrv,cPadrao,cFunCTB,cLote,/*nLinha*/,/*lExecuta*/,;
							/*cCriterio*/, /*lRateio*/, /*cChaveBusca*/, /*aCT5*/,;
							/*lPosiciona*/,@aFlagCTB,/*aTabRecOri*/,/*aDadosProva*/)

		if (nTotal>0)
			RodaProva(nHdlPrv,nTotal)
			lDigita:=.F.
			cA100Incl(cArquivo,nHdlPrv,3/*nOpcx*/,cLote,lDigita,.F./*lAglut*/,/*cOnLine*/,/*dData*/,/*dReproc*/, @aFlagCTB,/*aDadosProva*/,@aDiario)
		endif

	end sequence

	restArea(aArea)

	DEFAULT nTotal:=0

	return(nTotal)

static method isctbFIDC() class FIDC
	
	local cSA1IdxKey	as character
	
	local lCTBFIDC 		as logical
	local lbcoIsFIDC	as logical

	local nSA1RecNo 	as numeric
	local nSE1RecNo		as numeric
	
	begin sequence

		cSA1IdxKey:="A1_FILIAL+A1_COD+A1_LOJA"
		SA1->(dbSetOrder(retOrder("SA1",cSA1IdxKey)))
		if (SA1->(MsSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA)))
			nSA1RecNo:=SA1->(recNo())
			FIDC():setFIDCVar("nSA1RecNo",nSA1RecNo)
		endif

		nSE1RecNo:=SE1->(recNo())

		FIDC():setFIDCVar("nSE1RecNo",nSE1RecNo)
		FIDC():setFIDCVar("cBanco",SE1->E1_PORTADO)
		FIDC():setFIDCVar("cAgencia",SE1->E1_AGEDEP)
		FIDC():setFIDCVar("cConta",SE1->E1_CONTA)

		lbcoIsFIDC:=FIDC():bcoIsFIDC()
		FIDC():setFIDCVar("lbcoIsFIDC",lbcoIsFIDC)

		if (!lbcoIsFIDC)
			lCTBFIDC:=.F.
			break
		endif

		lCTBFIDC:=Stacktools():IsInCallStack("CleanBorSE1")
		if (lCTBFIDC)
			FIDC():setFIDCVar("cCTBStack","CleanBorSE1")
			break
		endif
		
		lCTBFIDC:=Stacktools():IsInCallStack("ExecMovFin")
		if (lCTBFIDC)
			FIDC():setFIDCVar("cCTBStack","ExecMovFin")
			break
		endif

		lCTBFIDC:=Stacktools():IsInCallStack("ExecBaixaCR")
		if (lCTBFIDC)
			FIDC():setFIDCVar("cCTBStack","ExecBaixaCR")
			break
		endif

		lCTBFIDC:=Stacktools():IsInCallStack("ctbFIDCOffLine")
		if (lCTBFIDC)
			FIDC():setFIDCVar("cCTBStack","ctbFIDCOffLine")
			break
		endif

		if (Stacktools():IsInCallStack("FINA070"))
			FIDC():setFIDCVar("cCTBStack","FINA070")
			FIDC():setFIDCVar("lFINA070",.T.)
		endif

		lCTBFIDC:=lbcoIsFIDC

	end sequence

	return(lCTBFIDC)

static method isLPctbFIDC(cPadrao) class FIDC	

	local lisLPctbFIDC as logical

	DEFAULT cPadrao:=FIDC():getFIDCVar("cPadrao","")
	paramtype cPadrao as character optional

	begin sequence

		cPadrao:=AllTrim(cPadrao)

		lisLPctbFIDC:=(cPadrao$FIDC():getBiaPar("FIDC_LP_BAIXA","FBX"))
		if (lisLPctbFIDC)
			break
		endif

		lisLPctbFIDC:=(cPadrao$FIDC():getBiaPar("FIDC_LP_BORDERO","FBO"))
		if (lisLPctbFIDC)
			break
		endif

		lisLPctbFIDC:=(cPadrao$"520|521|527")
		if (lisLPctbFIDC)
			break
		endif

	end sequence

	return(lisLPctbFIDC)

static method ctbFIDCOffLine() class FIDC

	local aArea				as array
	local aParamBox			as array 
	local aParamRet			as array

	local bAction			as block

	local cPadrao			as character
	local cPadraoBX			as character
	local cParamTit     	as character

	local dSvDataBase		as date

	local lPadrao			as logical
	local lParamBox			as logical
	local lctbFIDCOffLine	as logical

	local nPBoxAT			as numeric

	local otMultProcess		as object

	aArea:=getArea()

	dSvDataBase:=&("dDataBase")

    saveInter()
	
	FIDC():resetFIDCVars()

	begin sequence

        aParamBox:=array(0)
	    
		//01----------------------------------------------------------------------------------------------
		aAdd(aParamBox,array(9))
		nPBoxAT:=Len(aParamBox)
		aParamBox[nPBoxAT][1]:=1//[1]:1 - MsGet
		aParamBox[nPBoxAT][2]:="Número Borderô De"//[2]:Descricao
		aParamBox[nPBoxAT][3]:=Space(GetSx3Cache("E1_NUMBOR","X3_TAMANHO"))//[3]:String contendo o inicializador do campo
		aParamBox[nPBoxAT][4]:=GetSx3Cache("E1_NUMBOR","X3_PICTURE")//[4]:String contendo a Picture do campo
		aParamBox[nPBoxAT][5]:="AllWaysTrue()" //[5]:String contendo a validacao
		aParamBox[nPBoxAT][6]:=""//[6]:Consulta F3
		aParamBox[nPBoxAT][7]:="AllWaysTrue()"//[7]:String contendo a validacao When
		aParamBox[nPBoxAT][8]:=GetSx3Cache("E1_NUMBOR","X3_TAMANHO")//[8]:Tamanho do MsGet
		aParamBox[nPBoxAT][9]:=.F.//[9]:Flag .T./.F. Parametro Obrigatorio ?

		//02----------------------------------------------------------------------------------------------
		aAdd(aParamBox,array(9))
		nPBoxAT:=Len(aParamBox)
		aParamBox[nPBoxAT][1]:=1//[1]:1 - MsGet
		aParamBox[nPBoxAT][2]:="Número Borderô Ate"//[2]:Descricao
		aParamBox[nPBoxAT][3]:=Space(GetSx3Cache("E1_NUMBOR","X3_TAMANHO"))//[3]:String contendo o inicializador do campo
		aParamBox[nPBoxAT][4]:=GetSx3Cache("E1_NUMBOR","X3_PICTURE")//[4]:String contendo a Picture do campo
		aParamBox[nPBoxAT][5]:="AllWaysTrue()" //[5]:String contendo a validacao
		aParamBox[nPBoxAT][6]:=""//[6]:Consulta F3
		aParamBox[nPBoxAT][7]:="AllWaysTrue()"//[7]:String contendo a validacao When
		aParamBox[nPBoxAT][8]:=GetSx3Cache("E1_NUMBOR","X3_TAMANHO")//[8]:Tamanho do MsGet
		aParamBox[nPBoxAT][9]:=.T.//[9]:Flag .T./.F. Parametro Obrigatorio ?

		//03----------------------------------------------------------------------------------------------
		aAdd(aParamBox,array(9))
		nPBoxAT:=Len(aParamBox)
		aParamBox[nPBoxAT][1]:=1//[1]:1 - MsGet
		aParamBox[nPBoxAT][2]:="Data Borderô De"//[2]:Descricao
		aParamBox[nPBoxAT][3]:=CtoD("")//[3]:String contendo o inicializador do campo
		aParamBox[nPBoxAT][4]:="@D"//[4]:String contendo a Picture do campo
		aParamBox[nPBoxAT][5]:="AllWaysTrue()" //[5]:String contendo a validacao
		aParamBox[nPBoxAT][6]:=""//[6]:Consulta F3
		aParamBox[nPBoxAT][7]:="AllWaysTrue()"//[7]:String contendo a validacao When
		aParamBox[nPBoxAT][8]:=GetSx3Cache("E1_DATABOR","X3_TAMANHO")//[8]:Tamanho do MsGet
		aParamBox[nPBoxAT][9]:=.F.//[9]:Flag .T./.F. Parametro Obrigatorio ?

		//04----------------------------------------------------------------------------------------------
		aAdd(aParamBox,array(9))
		nPBoxAT:=Len(aParamBox)
		aParamBox[nPBoxAT][1]:=1//[1]:1 - MsGet
		aParamBox[nPBoxAT][2]:="Data Borderô Ate"//[2]:Descricao
		aParamBox[nPBoxAT][3]:=date()//[3]:String contendo o inicializador do campo
		aParamBox[nPBoxAT][4]:="@D"//[4]:String contendo a Picture do campo
		aParamBox[nPBoxAT][5]:="AllWaysTrue()" //[5]:String contendo a validacao
		aParamBox[nPBoxAT][6]:=""//[6]:Consulta F3
		aParamBox[nPBoxAT][7]:="AllWaysTrue()"//[7]:String contendo a validacao When
		aParamBox[nPBoxAT][8]:=GetSx3Cache("E1_DATABOR","X3_TAMANHO")//[8]:Tamanho do MsGet
		aParamBox[nPBoxAT][9]:=.T.//[9]:Flag .T./.F. Parametro Obrigatorio ?

		//05----------------------------------------------------------------------------------------------
		aAdd(aParamBox,array(9))
		nPBoxAT:=Len(aParamBox)
		aParamBox[nPBoxAT][1]:=1//[1]:1 - MsGet
		aParamBox[nPBoxAT][2]:="Lançamento Padrão"//[2]:Descricao
		aParamBox[nPBoxAT][3]:=FIDC():getBiaPar("FIDC_LP_BORDERO",Space(GetSx3Cache("CT5_LANPAD","X3_TAMANHO")))//[3]:String contendo o inicializador do campo
		aParamBox[nPBoxAT][4]:=GetSx3Cache("CT5_LANPAD","X3_PICTURE")//[4]:String contendo a Picture do campo
		aParamBox[nPBoxAT][5]:="ExistCpo('CT5')" //[5]:String contendo a validacao
		aParamBox[nPBoxAT][6]:="CT5"//[6]:Consulta F3
		aParamBox[nPBoxAT][7]:="AllWaysTrue()"//[7]:String contendo a validacao When
		aParamBox[nPBoxAT][8]:=GetSx3Cache("CT5_LANPAD","X3_TAMANHO")//[8]:Tamanho do MsGet
		aParamBox[nPBoxAT][9]:=.T.//[9]:Flag .T./.F. Parametro Obrigatorio ?

		cPadraoBX:=FIDC():getBiaPar("FIDC_LP_BAIXA","FBX")

		//06----------------------------------------------------------------------------------------------
		aAdd(aParamBox,array(9))
		nPBoxAT:=Len(aParamBox)
		aParamBox[nPBoxAT][1]:=1//[1]:1 - MsGet
		aParamBox[nPBoxAT][2]:="Data Retorno (Baixa)"//[2]:Descricao
		aParamBox[nPBoxAT][3]:=Date()//[3]:String contendo o inicializador do campo
		aParamBox[nPBoxAT][4]:="@D"//[4]:String contendo a Picture do campo
		aParamBox[nPBoxAT][5]:="" //[5]:String contendo a validacao
		aParamBox[nPBoxAT][6]:=""//[6]:Consulta F3
		aParamBox[nPBoxAT][7]:="'"+cPadraoBX+"'$MV_PAR05"//[7]:String contendo a validacao When
		aParamBox[nPBoxAT][8]:=GetSx3Cache("E1_DATABOR","X3_TAMANHO")//[8]:Tamanho do MsGet
		aParamBox[nPBoxAT][9]:=.T.//[9]:Flag .T./.F. Parametro Obrigatorio ?

        cParamTit:="CTB FIDC OffLine"
        lParamBox:=ParamBox(@aParamBox,@cParamTit,@aParamRet,/*bOk*/,/*aButtons*/,.T./*lCentered*/,/*nPosx*/,/*nPosy*/,/*oDlgWizard*/,/*cLoad*/,.T./*lCanSave*/,.T./*lUserSave*/)

        if (!lParamBox)
            lctbFIDCOffLine:=lParamBox
			break
        endif

		cPadrao:=aParamRet[5]
		FIDC():setFIDCVar("cPadrao",cPadrao)

		lctbFIDCOffLine:=FIDC():isLPctbFIDC()
		if (!lctbFIDCOffLine)
			ApMsgAlert("Lançamento Padrão ["+cPadrao+"] Inválido. Informe um LP válido para contabilização FIDC.",cParamTit)
			break
		endif

		lPadrao:=verPadrao(cPadrao)
		if (!lPadrao)
			lctbFIDCOffLine:=.F.
			ApMsgAlert("Lançamento Padrão ["+cPadrao+"] Inválido. Informe um LP válido para contabilização FIDC.",cParamTit)
			break
		endif

		bAction:={||lctbFIDCOffLine:=ctbFIDCOffLine(otMultProcess,aParamRet)}
		otMultProcess:=tMultProcess():New(bAction,cParamTit,"Aguarde. Contabilizando",nil,1)
		otMultProcess:Activate()

	end sequence

	FIDC():resetFIDCVars()
	
	restInter()

	&("dDataBase"):=dSvDataBase

	restArea(aArea)

	return(lctbFIDCOffLine)

static function ctbFIDCOffLine(otMultProcess as object,aParamRet as array) as logical

	local aSE1RecNos		as array
	local aZK4RecNos		as array
	
	local cPadrao			as character
	local cPadraoBX			as character	
	
	local cTmpAlias			as character
	
	local cSA1IdxKey		as character
	local cSA1Filial		as character
	
	local cE1NUMBORDe		as character
	local cE1NUMBORAte		as character
	local cE1DATABORDe		as character
	local cE1DATABORAte		as character

	local cZK4Data			as character

	local dE1DATABORDe		as date
	local dE1DATABORAte		as date

	local dZK4Data			as date
	
	local lBaixa 			as logical
	local lUsaFlag			as logical
	local lUsaSeqCor		as logical
	local lctbFIDCOffLine	as logical

	local nRecNo			as numeric
	local nRecNos			as numeric
	
	local nSA1RecNo			as numeric
	local nSE1RecNo			as numeric
	local nZK4RecNo			as numeric
	
	local nSA1IdxKey		as numeric

	begin sequence

		cPadrao:=aParamRet[5]
		cPadraoBX:=FIDC():getBiaPar("FIDC_LP_BAIXA","FBX")

		cTmpAlias:=getNextAlias()
		
		lBaixa:=(cPadrao==cPadraoBX)
		if (lBaixa)

			cE1NUMBORDe:=Space(GetSX3Cache("E1_NUMBOR","X3_TAMANHO"))
			cE1NUMBORAte:=Replicate("Z",GetSX3Cache("E1_NUMBOR","X3_TAMANHO"))

			cE1DATABORDe:=Space(GetSX3Cache("E1_DATABOR","X3_TAMANHO"))
			cE1DATABORAte:=Replicate("Z",GetSX3Cache("E1_DATABOR","X3_TAMANHO"))

			dZK4Data:=aParamRet[6]
			cZK4Data:=DToS(dZK4Data)

			beginSQL alias cTmpAlias
				SELECT SE1.R_E_C_N_O_ SE1RECNO
				      ,ZK4.R_E_C_N_O_ ZK4RECNO
				  FROM %table:SE1% SE1 (NOLOCK)
				  JOIN %table:ZK4% ZK4 (NOLOCK) 
				    ON (
							SE1.E1_PORTADO=ZK4.ZK4_BANCO
				    	AND SE1.E1_AGEDEP=ZK4.ZK4_AGENCI
				    	AND SE1.E1_CONTA=ZK4.ZK4_CONTA
						AND SE1.E1_NUM+E1_PARCELA=ZK4.ZK4_NUMERO
				    	AND SE1.E1_NUMBCO=SUBSTRING(ZK4.ZK4_NOSNUM,1,11)
				    	AND SE1.E1_VENCTO=ZK4.ZK4_DTVENC
					)				 
					,%table:SA6% SA6 (NOLOCK)
				WHERE SE1.%notDel%
				  AND ZK4.%notDel%
				  AND SE1.E1_FILIAL=%xFilial:SE1%
				  AND ZK4.ZK4_FILIAL=%xFilial:ZK4%
				  AND ZK4.ZK4_DATA=%exp:cZK4Data%
				  AND ZK4.ZK4_CODOCO='06'
				  AND ZK4.ZK4_TIPO='R'
				  AND SE1.E1_LA<>'S'
				  AND SE1.E1_PORTADO=SA6.A6_COD
				  AND SE1.E1_CONTA=SA6.A6_NUMCON
				  AND SE1.E1_AGEDEP=SA6.A6_AGENCIA
				  AND SE1.E1_NUMBOR BETWEEN %exp:cE1NUMBORDe% AND %exp:cE1NUMBORAte%
				  AND SE1.E1_DATABOR BETWEEN %exp:cE1DATABORDe% AND %exp:cE1DATABORAte%
				  AND SA6.%notDel%
				  AND SA6.A6_FILIAL=%xFilial:SA6%
				  AND SA6.A6_YTPINTB='1'
				  AND SE1.E1_YSITAPI='2'
			endSQL

		else

			cE1NUMBORDe:=aParamRet[1]
			cE1NUMBORAte:=aParamRet[2]

			dE1DATABORDe:=aParamRet[3]
			dE1DATABORAte:=aParamRet[4]

			cE1DATABORDe:=DToS(dE1DATABORDe)
			cE1DATABORAte:=DToS(dE1DATABORAte)

			beginSQL alias cTmpAlias
				SELECT SE1.R_E_C_N_O_ SE1RECNO
				  FROM %table:SE1% SE1 (NOLOCK)
				   	  ,%table:SA6% SA6 (NOLOCK)
				 WHERE SE1.%notDel%
				   AND SE1.E1_FILIAL=%xFilial:SE1%
				   AND SE1.E1_LA<>'S'
				   AND SE1.E1_PORTADO=SA6.A6_COD
				   AND SE1.E1_CONTA=SA6.A6_NUMCON
				   AND SE1.E1_AGEDEP=SA6.A6_AGENCIA
				   AND SE1.E1_NUMBOR BETWEEN %exp:cE1NUMBORDe% AND %exp:cE1NUMBORAte%
				   AND SE1.E1_DATABOR BETWEEN %exp:cE1DATABORDe% AND %exp:cE1DATABORAte%
				   AND SA6.%notDel%
				   AND SA6.A6_FILIAL=%xFilial:SA6%
				   AND SA6.A6_YTPINTB='1'
				   AND SE1.E1_YSITAPI='2'
			endSQL

		endif

		aSE1RecNos:=array(0)
		if (lBaixa)
			aZK4RecNos:=array(0)
		endif

		while (cTmpAlias)->(!eof())
			nSE1RecNo:=(cTmpAlias)->SE1RECNO
			aAdd(aSE1RecNos,nSE1RecNo)
			if (lBaixa)
				nZK4RecNo:=(cTmpAlias)->ZK4RECNO
				aAdd(aZK4RecNos,nZK4RecNo)
			endif
			(cTmpAlias)->(dbSkip())
		end while

		(cTmpAlias)->(dbCloseArea())
		dbSelectArea("SE1")

		nRecNos:=Len(aSE1RecNos)
		otMultProcess:SetRegua(1,nRecNos)

		cSA1IdxKey:="A1_FILIAL+A1_COD+A1_LOJA"
		nSA1IdxKey:=retOrder("SA1",cSA1IdxKey)

		cSA1Filial:=xFilial("SA1")

		lUsaFlag:=SuperGetMV("MV_CTBFLAG",.F./*lHelp*/,.F./*DEFAULT*/)
		lUsaSeqCor:=FindFunction("UsaSeqCor")

		for nRecNo:=1 to nRecNos
			otMultProcess:IncRegua(1,"FIDC :: Contabilizando ["+cPadrao+"]. Aguarde...")
			nSE1RecNo:=aSE1RecNos[nRecNo]
			SE1->(dbGoTo(nSE1RecNo))
			if (lBaixa)
				nZK4RecNo:=aZK4RecNos[nRecNo]
				ZK4->(dbGoTo(nZK4RecNo))
				&("dDataBase"):=ZK4->ZK4_DTLIQ
			else
				&("dDataBase"):=SE1->E1_DATABOR
			endif
			SA1->(dbSetOrder(nSA1IdxKey))
			if (SA1->(!MsSeek(cSA1Filial+SE1->E1_CLIENTE+SE1->E1_LOJA)))
				loop
			endif
			nSA1RecNo:=SA1->(recNo())
			FIDC():setFIDCVar("lCTBFIDC",.T.)
			FIDC():setFIDCVar("cCTBStack","ctbFIDCOffLine")
			FIDC():setFIDCVar("cPadrao",cPadrao)
			FIDC():setFIDCVar("nSE1RecNo",nSE1RecNo)
			FIDC():setFIDCVar("nSA1RecNo",nSA1RecNo)
			FIDC():setFIDCVar("cBanco",SE1->E1_PORTADO)
			FIDC():setFIDCVar("cAgencia",SE1->E1_AGEDEP)
			FIDC():setFIDCVar("cConta",SE1->E1_CONTA)
			FIDC():setFIDCVar("lUsaFlag",lUsaFlag)
			if (FIDC():getFIDCVar("lUsaFlag",.F.))
				FIDC():setFIDCVar("aFlagCTB",{"E1_LA","S","SE1",nSE1RecNo,0,0,0})
			endif
			FIDC():setFIDCVar("lDiario",(lUsaSeqCor.and.UsaSeqCor()))
			if (FIDC():getFIDCVar("lDiario",.F.))
				FIDC():setFIDCVar("aDiario",{"SE1",nSE1RecNo,SE1->E1_DIACTB,"E1_NODIA","E1_DIACTB"})
			endif
			SE1->(FIDC():ctbFIDC())
			FIDC():resetFIDCVars()
		next nRecNo

		lctbFIDCOffLine:=.T.

	end sequence

	return(lctbFIDCOffLine)

static method txFIDCOffLine() class FIDC

	local aArea				as array
	local aParamBox			as array 
	local aParamRet			as array

	local bAction			as block

	local cParamTit     	as character

	local lParamBox			as logical
	local ltxFIDCOffLine	as logical

	local nPBoxAT			as numeric

	local otMultProcess		as object

	aArea:=getArea()

    saveInter()
	
	FIDC():resetFIDCVars()

	begin sequence

        aParamBox:=array(0)
	    
		//01----------------------------------------------------------------------------------------------
		aAdd(aParamBox,array(9))
		nPBoxAT:=Len(aParamBox)
		aParamBox[nPBoxAT][1]:=1//[1]:1 - MsGet
		aParamBox[nPBoxAT][2]:="Número Borderô De"//[2]:Descricao
		aParamBox[nPBoxAT][3]:=Space(GetSx3Cache("E1_NUMBOR","X3_TAMANHO"))//[3]:String contendo o inicializador do campo
		aParamBox[nPBoxAT][4]:=GetSx3Cache("E1_NUMBOR","X3_PICTURE")//[4]:String contendo a Picture do campo
		aParamBox[nPBoxAT][5]:="AllWaysTrue()" //[5]:String contendo a validacao
		aParamBox[nPBoxAT][6]:=""//[6]:Consulta F3
		aParamBox[nPBoxAT][7]:="AllWaysTrue()"//[7]:String contendo a validacao When
		aParamBox[nPBoxAT][8]:=GetSx3Cache("E1_NUMBOR","X3_TAMANHO")//[8]:Tamanho do MsGet
		aParamBox[nPBoxAT][9]:=.F.//[9]:Flag .T./.F. Parametro Obrigatorio ?

		//02----------------------------------------------------------------------------------------------
		aAdd(aParamBox,array(9))
		nPBoxAT:=Len(aParamBox)
		aParamBox[nPBoxAT][1]:=1//[1]:1 - MsGet
		aParamBox[nPBoxAT][2]:="Número Borderô Ate"//[2]:Descricao
		aParamBox[nPBoxAT][3]:=Space(GetSx3Cache("E1_NUMBOR","X3_TAMANHO"))//[3]:String contendo o inicializador do campo
		aParamBox[nPBoxAT][4]:=GetSx3Cache("E1_NUMBOR","X3_PICTURE")//[4]:String contendo a Picture do campo
		aParamBox[nPBoxAT][5]:="AllWaysTrue()" //[5]:String contendo a validacao
		aParamBox[nPBoxAT][6]:=""//[6]:Consulta F3
		aParamBox[nPBoxAT][7]:="AllWaysTrue()"//[7]:String contendo a validacao When
		aParamBox[nPBoxAT][8]:=GetSx3Cache("E1_NUMBOR","X3_TAMANHO")//[8]:Tamanho do MsGet
		aParamBox[nPBoxAT][9]:=.T.//[9]:Flag .T./.F. Parametro Obrigatorio ?

		//03----------------------------------------------------------------------------------------------
		aAdd(aParamBox,array(9))
		nPBoxAT:=Len(aParamBox)
		aParamBox[nPBoxAT][1]:=1//[1]:1 - MsGet
		aParamBox[nPBoxAT][2]:="Data Borderô De"//[2]:Descricao
		aParamBox[nPBoxAT][3]:=CtoD("")//[3]:String contendo o inicializador do campo
		aParamBox[nPBoxAT][4]:=GetSx3Cache("E1_EMISSAO","X3_PICTURE")//[4]:String contendo a Picture do campo
		aParamBox[nPBoxAT][5]:="AllWaysTrue()" //[5]:String contendo a validacao
		aParamBox[nPBoxAT][6]:=""//[6]:Consulta F3
		aParamBox[nPBoxAT][7]:="AllWaysTrue()"//[7]:String contendo a validacao When
		aParamBox[nPBoxAT][8]:=GetSx3Cache("E1_EMISSAO","X3_TAMANHO")//[8]:Tamanho do MsGet
		aParamBox[nPBoxAT][9]:=.F.//[9]:Flag .T./.F. Parametro Obrigatorio ?

		//04----------------------------------------------------------------------------------------------
		aAdd(aParamBox,array(9))
		nPBoxAT:=Len(aParamBox)
		aParamBox[nPBoxAT][1]:=1//[1]:1 - MsGet
		aParamBox[nPBoxAT][2]:="Data Borderô Ate"//[2]:Descricao
		aParamBox[nPBoxAT][3]:=date()//[3]:String contendo o inicializador do campo
		aParamBox[nPBoxAT][4]:=GetSx3Cache("E1_EMISSAO","X3_PICTURE")//[4]:String contendo a Picture do campo
		aParamBox[nPBoxAT][5]:="AllWaysTrue()" //[5]:String contendo a validacao
		aParamBox[nPBoxAT][6]:=""//[6]:Consulta F3
		aParamBox[nPBoxAT][7]:="AllWaysTrue()"//[7]:String contendo a validacao When
		aParamBox[nPBoxAT][8]:=GetSx3Cache("E1_EMISSAO","X3_TAMANHO")//[8]:Tamanho do MsGet
		aParamBox[nPBoxAT][9]:=.T.//[9]:Flag .T./.F. Parametro Obrigatorio ?

        cParamTit:="Taxa FIDC OffLine"
        lParamBox:=ParamBox(@aParamBox,@cParamTit,@aParamRet,/*bOk*/,/*aButtons*/,.T./*lCentered*/,/*nPosx*/,/*nPosy*/,/*oDlgWizard*/,/*cLoad*/,.T./*lCanSave*/,.T./*lUserSave*/)

        if (!lParamBox)
            ltxFIDCOffLine:=lParamBox
			break
        endif

		bAction:={||ltxFIDCOffLine:=txFIDCOffLine(otMultProcess,aParamRet)}
		otMultProcess:=tMultProcess():New(bAction,cParamTit,"Aguarde. Atualizando Taxa...",nil,1)
		otMultProcess:Activate()

	end sequence

	FIDC():resetFIDCVars()
	
	restInter()

	restArea(aArea)

	return(ltxFIDCOffLine)

static function txFIDCOffLine(otMultProcess as object,aParamRet as array) as logical

	local aSE1RecNos		as array
	
	local cTmpAlias			as character

	local cE1NUMBORDe		as character
	local cE1NUMBORAte		as character
	local cE1DATABORDe		as character
	local cE1DATABORAte		as character

	local dE1DATABORDe		as date
	local dE1DATABORAte		as date
	
	local ltxFIDCOffLine	as logical

	local nRecNo			as numeric
	local nRecNos			as numeric
	local nSE1RecNo			as numeric

	begin sequence

		cE1NUMBORDe:=aParamRet[1]
		cE1NUMBORAte:=aParamRet[2]

		dE1DATABORDe:=aParamRet[3]
		dE1DATABORAte:=aParamRet[4]

		cE1DATABORDe:=DToS(dE1DATABORDe)
		cE1DATABORAte:=DToS(dE1DATABORAte)

		cTmpAlias:=getNextAlias()
		
		beginSQL alias cTmpAlias
			SELECT SE1.R_E_C_N_O_ SE1RECNO
			FROM %table:SE1% SE1 (NOLOCK)
				,%table:SA6% SA6 (NOLOCK)
			WHERE SE1.%notDel%
			  AND SE1.E1_FILIAL=%xFilial:SE1%
			  AND SE1.E1_PORTADO=SA6.A6_COD
			  AND SE1.E1_CONTA=SA6.A6_NUMCON
			  AND SE1.E1_AGEDEP=SA6.A6_AGENCIA
			  AND SE1.E1_NUMBOR BETWEEN %exp:cE1NUMBORDe% AND %exp:cE1NUMBORAte%
			  AND SE1.E1_DATABOR BETWEEN %exp:cE1DATABORDe% AND %exp:cE1DATABORAte%
			  AND SA6.%notDel%
			  AND SA6.A6_FILIAL=%xFilial:SA6%
			  AND SA6.A6_YTPINTB='1'
		endSQL

		aSE1RecNos:=array(0)

		while (cTmpAlias)->(!eof())
			nSE1RecNo:=(cTmpAlias)->SE1RECNO
			aAdd(aSE1RecNos,nSE1RecNo)
			(cTmpAlias)->(dbSkip())
		end while

		(cTmpAlias)->(dbCloseArea())
		dbSelectArea("SE1")

		nRecNos:=Len(aSE1RecNos)
		otMultProcess:SetRegua(1,nRecNos)

		for nRecNo:=1 to nRecNos
			otMultProcess:IncRegua(1,"FIDC :: Atualizando Taxa. Aguarde...")
			nSE1RecNo:=aSE1RecNos[nRecNo]
			SE1->(dbGoTo(nSE1RecNo))
			if SE1->(recLock("SE1",.F.))
				FIDC():setFIDCVar("cBanco",SE1->E1_PORTADO)
				FIDC():setFIDCVar("cAgencia",SE1->E1_AGEDEP)
				FIDC():setFIDCVar("cConta",SE1->E1_CONTA)
				if (FIDC():BCOIsFIDC())
					SE1->E1_YFDCPER:=FIDC():percentualDesconto() 
					SE1->E1_YFDCVAL:=FIDC():calculaDesconto(SE1->E1_VALOR+SE1->E1_YTXCOBR,SE1->E1_VENCTO,SE1->E1_DATABOR)
				endif
				SE1->(msUnLock())
			endif
		next nRecNo

		ltxFIDCOffLine:=.T.

	end sequence

	return(ltxFIDCOffLine)

static method setFIDCVar(uVar,uVal) class FIDC
    FIDCVarsInit()
    return(oFIDCVars:Set(uVar,uVal))

static method getFIDCVar(uVar,uDefault) class FIDC
    FIDCVarsInit()
    return(oFIDCVars:Get(uVar,uDefault))

static method resetFIDCVars() class FIDC
    FIDCVarsInit()
    return(oFIDCVars:Clear())

static function FIDCVarsInit()
    DEFAULT oFIDCVars:=JSONArray():New()
    return(oFIDCVars)

static method percentualDesconto(cEmp,cFil) class FIDC
	local nPercentualDesconto as numeric
	paramtype cEmp as character optional
	paramtype cFil as character optional
	cEmp:=PadR(cEmp,getSX3Cache("ZL4_CODEMP","X3_TAMANHO"))
	cFil:=PadR(cFil,getSX3Cache("ZL4_CODFIL","X3_TAMANHO"))
	nPercentualDesconto:=FIDC():getBiaPar("FIDC_PERCENTUAL_DESCONTO",0,cEmp,cFil)
	nPercentualDesconto/=100
	return(nPercentualDesconto)

static method calculaDesconto(nValor,dVencimento,dReferencia) class FIDC

	local dToday			as date
	local dDataValida		as date
	
	local nDiasCalculo		as numeric
	local nFatorCalculo		as numeric
	local nValorDesconto	as numeric
	local nFIDCPercentual	as numeric

	paramtype nValor		as numeric
	paramtype dVencimento	as date
	paramtype dReferencia	as date optional

	dToday:=Date()
	DEFAULT dReferencia:=dToday
	
	nFIDCPercentual:=FIDC():percentualDesconto() 
	FIDC():setFIDCVar("nFIDCPercentual",nFIDCPercentual)
	
	dDataValida:=DataValida(DataValida(dVencimento)+1)
	FIDC():setFIDCVar("dDataValida",dDataValida)
	
	nDiasCalculo:=(dDataValida-dReferencia)
	FIDC():setFIDCVar("nDiasCalculo",nDiasCalculo)
	
	nFatorCalculo:=(((nFIDCPercentual+1)^(nDiasCalculo/30))-1)
	FIDC():setFIDCVar("nFatorCalculo",nFatorCalculo)
	
	nValorDesconto:=(nValor*nFatorCalculo)
	nValorDesconto:=Round(nValorDesconto,2)
	FIDC():setFIDCVar("nValorDesconto",nValorDesconto)

	return(nValorDesconto)

static method getBiaPar(cParam,xDefault,cEmp,cFil) class FIDC

	local cFIDCPar as character

	local uRet

	paramtype cParam as character
	paramtype cEmp as character optional
	paramtype cFil as character optional

	FIDCParsInit()

	cParam:=PadR(cParam,getSX3Cache("ZL4_PARAM","X3_TAMANHO"))

	cEmp:=PadR(cEmp,getSX3Cache("ZL4_CODEMP","X3_TAMANHO"))
	cFil:=PadR(cFil,getSX3Cache("ZL4_CODFIL","X3_TAMANHO"))

	cFIDCPar:=(cEmp+cFil+cParam)

	if ((!stacktools():IsInCallStack("existBiaPar")).and.FIDC():existBiaPar(cParam,cEmp,cFil,xDefault))
		uRet:=oFIDCPars:Get(cFIDCPar,xDefault)
	else
		uRet:=U_GETBIAPAR(@cParam,@xDefault,@cEmp,@cFil)
		oFIDCPars:Set(cFIDCPar,uRet)
	endif

	return(uRet)

static method putBiaPar(cParam,cConteudo,cEmp,cFil) class FIDC

	paramtype cParam as character
	paramtype cConteudo as character
	paramtype cEmp as character optional
	paramtype cFil as character optional

	FIDCParsInit()

	cParam:=PadR(cParam,getSX3Cache("ZL4_PARAM","X3_TAMANHO"))

	cEmp:=PadR(cEmp,getSX3Cache("ZL4_CODEMP","X3_TAMANHO"))
	cFil:=PadR(cFil,getSX3Cache("ZL4_CODFIL","X3_TAMANHO"))

	return(U_PUTBIAPAR(@cParam,@cConteudo,@cEmp,@cFil))

static method existBiaPar(cParam,cEmp,cFil,xDefault) class FIDC

	local aArea			as array

	local cAlias		as character
	local cZL4CODEMP	as character
	local cZL4CODFIL	as character

	local cFIDCPar		as character

	local lexistBiaPar	as logical

	local nZL4RecNo		as numeric

	local uValue

	paramtype cParam as character
	DEFAULT cEmp:=Space(getSX3Cache("ZL4_CODEMP","X3_TAMANHO"))
	paramtype cEmp as character optional
	DEFAULT cFil:=Space(getSX3Cache("ZL4_CODFIL","X3_TAMANHO"))
	paramtype cFil as character optional

	cEmp:=PadR(cEmp,getSX3Cache("ZL4_CODEMP","X3_TAMANHO"))
	cFil:=PadR(cFil,getSX3Cache("ZL4_CODFIL","X3_TAMANHO"))
	cParam:=PadR(cParam,getSX3Cache("ZL4_PARAM","X3_TAMANHO"))

	FIDCParsInit()

	begin sequence

		cFIDCPar:=(cEmp+cFil+cParam)

		lexistBiaPar:=(oFIDCPars:GetAT(cFIDCPar)>0)
		if (lexistBiaPar)
			break
		endif

		aArea:=getArea()

		cAlias:=getNextAlias()

		beginSQL alias cAlias

			SELECT ZL4.R_E_C_N_O_ ZL4RECNO
			  FROM %table:ZL4% ZL4
			 WHERE ZL4.%notDel%
			   AND ZL4.ZL4_FILIAL=%xFilial:ZL4%
			   AND (
			   			(ZL4.ZL4_CODEMP=%exp:cEmp% AND ZL4.ZL4_CODFIL=%exp:cFil%)
						OR
						(ZL4.ZL4_CODEMP=%exp:cEmp% AND ZL4.ZL4_CODFIL=' ')
						OR
						(ZL4.ZL4_CODEMP=' ' AND ZL4.ZL4_CODFIL=%exp:cFil%)
			   )

		endSQL

		while (cAlias)->(!eof())
			nZL4RecNo:=(cAlias)->ZL4RECNO
			ZL4->(dbGoTo(nZL4RecNo))
			cZL4CODEMP:=ZL4->ZL4_CODEMP
			cZL4CODFIL:=ZL4->ZL4_CODFIL
			uValue:=FIDC():getBiaPar(cParam,xDefault,cZL4CODEMP,cZL4CODFIL)
			cFIDCPar:=(cZL4CODEMP+cZL4CODFIL+cParam)
			oFIDCPars:Set(cFIDCPar,uValue)
			(cAlias)->(dbSkip())
		end while

		(cAlias)->(dbCloseArea())

		restArea(aArea)

		cFIDCPar:=(cEmp+cFil+cParam)
		lexistBiaPar:=(oFIDCPars:GetAT(cFIDCPar)>0)

	end sequence

	return(lexistBiaPar)

static method InsertBiaPar(cParam,cConteudo,cEmp,cFil) class FIDC

	local lInsertBiaPar as logical

	paramtype cParam as character
	paramtype cConteudo as character
	paramtype cEmp as character optional
	paramtype cFil as character optional

	FIDCParsInit()

	cEmp:=PadR(cEmp,getSX3Cache("ZL4_CODEMP","X3_TAMANHO"))
	cFil:=PadR(cFil,getSX3Cache("ZL4_CODFIL","X3_TAMANHO"))
	cParam:=PadR(cParam,getSX3Cache("ZL4_PARAM","X3_TAMANHO"))

	lInsertBiaPar:=FIDC():existBiaPar(@cParam,@cEmp,@cFil)
	if (!lInsertBiaPar)
		//TODO: Implementar a inclusao de parametros
	endif

	return(lInsertBiaPar)

static method resetFIDCPars() class FIDC
    FIDCParsInit()
    return(oFIDCPars:Clear())

static function FIDCParsInit()
    DEFAULT oFIDCPars:=JSONArray():New()
    return(oFIDCPars)

static method getQueryRem(aParamRet,aBCOIsFIDC,aFWTTables) class FIDC

    local aFields           as array

    local bError            as block
    local bErrorBlock       as block

    local cTmpAlias         as character
    local cTmpTable         as character
    local cAPITable         as character

    local cA6COD            as character
    local cA6AGENCIA        as character
    local cA6NUMCON         as character

    local cA1CODDe          as character
    local cA1CODAte         as character

    local cE1DATABORDe      as character
    local cE1DATABORAte     as character

    local cE1EMISSAODe      as character
    local cE1EMISSAOAte     as character

    local cE1VENCTODe       as character
    local cE1VENCTOAte      as character

    local cUnidadeCodigo    as character

    local cSQLPath          as character
    local cSQLFile          as character
    local cSQLQuery         as character
    local cSQLInsert        as character

    local cField            as character
	local cSQLFields		as character

    local nField            as numeric
    local nFields           as numeric

	local oFWTTable			as object

	paramtype aParamRet as array
	paramtype aBCOIsFIDC as array

	paramtype aFWTTables as array optional default array(0)

    cA6COD:=aBCOIsFIDC[3]
    cA6AGENCIA:=aBCOIsFIDC[4]
    cA6NUMCON:=aBCOIsFIDC[5]

    cA1CODDe:=aParamRet[1]
    cA1CODAte:=aParamRet[2]

    cE1DATABORDe:=DToS(aParamRet[3])
    cE1DATABORAte:=DToS(aParamRet[4])

    cE1EMISSAODe:=DToS(aParamRet[5])
    cE1EMISSAOAte:=DToS(aParamRet[6])

    cE1VENCTODe:=DToS(aParamRet[7])
    cE1VENCTOAte:=DToS(aParamRet[8])

    cAPITable:=A35():tmpTableName("api_table")
    cAPITable:="%"+cAPITable+"%"

    cTmpAlias:=getNextAlias()

    cUnidadeCodigo:=&("cEmpAnt")
    cUnidadeCodigo+=&("cFilAnt")

    if (IsBlind())
        cSQLPath:="\tmp\"
    else
        cSQLPath:=getTempPath()
        if (!right(cSQLPath,1)=="\")
            cSQLPath+="\"
        endif
    endif
    cSQLPath+="FIDC\SQL\"

    bError:={|oError|break(oError)}
    bErrorBlock:=ErrorBlock(bError)
    begin sequence

        beginSQL Alias cTmpAlias

            %noparser%

            WITH QAPI AS (
                                SELECT SE1RECNO=NumeroControleParticipante 
                                  FROM APIFINANCEIRO.dbo.Boleto  (NOLOCK)
                                  JOIN APIFINANCEIRO.dbo.Unidade (NOLOCK) ON (Unidade.ID=Boleto.UnidadeID)
                                  JOIN APIFINANCEIRO.dbo.Lote    (NOLOCK) ON (Lote.ID = Boleto.LoteID)
                                  JOIN APIFINANCEIRO.dbo.Cedente (NOLOCK) ON (Cedente.ID=Boleto.CedenteID)
                                  JOIN APIFINANCEIRO.dbo.ContaBancaria (NOLOCK) ON (ContaBancaria.ID=Cedente.ContaBancariaID)
                                    WHERE CodigoBanco=%exp:cA6COD%
                                      AND Agencia=%exp:cA6AGENCIA%
                                      AND Conta=%exp:cA6NUMCON%
                                      AND NomeArquivo<>''
                                      AND Unidade.Codigo=%exp:cUnidadeCodigo%
                                      AND CONVERT(DATE,APIFINANCEIRO.dbo.Boleto.InsertDate) BETWEEN CONVERT(DATE,%exp:cE1DATABORDe%) AND CONVERT(DATE,%exp:cE1DATABORAte%)
            )
            SELECT * INTO %exp:cAPITable% FROM QAPI
            
        endSQL

    end sequence
    ErrorBlock(bErrorBlock)
    
    cSQLQuery:=getLastQuery()[2]
    TCSQLExec(cSQLQuery)

    A35():writeSQLFile(@cSQLQuery,&("cEmpAnt"),&("cFilAnt"),"FIDCRelatorioRemessa","00","qry_insert",@cSQLPath,@cSQLFile)

    if (select(cTmpAlias)>0)
        (cTmpAlias)->(dbCloseArea())
    endif
    
	cSQLFields:=""
	cSQLFields+="SA1.A1_COD"
    cSQLFields+=",SA1.A1_NOME"
    cSQLFields+=",SE1.E1_DATABOR"
    cSQLFields+=",SE1.E1_PREFIXO+SE1.E1_NUM+SE1.E1_PARCELA AS P_NUM_PARC"
    cSQLFields+=",SE1.E1_EMISSAO"
    cSQLFields+=",SE1.E1_VENCTO"
    cSQLFields+=",0  AS DIAS_ANTEC"
    cSQLFields+=",'' AS E1_VENCREA"
    cSQLFields+=",SE1.E1_VALOR"
    cSQLFields+=",SE1.E1_YFDCVAL"
	cSQLFields+=",SE1.E1_NUMBCO"

	if (stacktools():IsInCallStack("getQueryCon"))
		cSQLFields+=",SE1.R_E_C_N_O_ AS SE1RECNO"
	endif

	cSQLFields:="%"+cSQLFields+"%"

    beginSQL alias cTmpAlias

        %noparser%

        COLUMN E1_DATABOR  AS DATE
        COLUMN E1_EMISSAO  AS DATE
        COLUMN E1_VENCTO   AS DATE
        COLUMN E1_VENCREA  AS DATE

        SELECT %exp:cSQLFields%
          FROM %table:SE1% SE1 (NOLOCK)
          JOIN %exp:cAPITable% QAPI ON (SE1.R_E_C_N_O_=QAPI.SE1RECNO)
          JOIN %table:SA1% SA1 (NOLOCK) ON (
                SA1.%notDel%
           AND SA1.A1_FILIAL=%xFilial:SA1%
           AND SE1.E1_CLIENTE=SA1.A1_COD
           AND SE1.E1_LOJA=SA1.A1_LOJA
          )
          JOIN %table:SA6% SA6 (NOLOCK) ON (
                SA6.%notDel%  
           AND SA6.A6_FILIAL=%xFilial:SA6%
           AND SA6.A6_COD=%exp:cA6COD%
           AND SA6.A6_AGENCIA=%exp:cA6AGENCIA%
           AND SA6.A6_NUMCON=%exp:cA6NUMCON%
           AND SE1.E1_PORTADO=SA6.A6_COD
           AND SE1.E1_AGEDEP=SA6.A6_AGENCIA
           AND SE1.E1_CONTA=SA6.A6_NUMCON
           AND SA6.A6_YTPINTB='1'
          )
         WHERE (1=2)
           AND SE1.%notDel%
           AND SA1.%notDel%
           AND SA6.%notDel%
           AND SE1.E1_FILIAL=%xFilial:SE1%
           AND SA1.A1_FILIAL=%xFilial:SA1%
           AND SA6.A6_FILIAL=%xFilial:SA6%
           AND SE1.E1_CLIENTE=SA1.A1_COD
           AND SE1.E1_LOJA=SA1.A1_LOJA
           AND SA6.A6_COD=%exp:cA6COD%
           AND SA6.A6_AGENCIA=%exp:cA6AGENCIA%
           AND SA6.A6_NUMCON=%exp:cA6NUMCON%
           AND SE1.E1_PORTADO=SA6.A6_COD
           AND SE1.E1_AGEDEP=SA6.A6_AGENCIA
           AND SE1.E1_CONTA=SA6.A6_NUMCON
           AND SA6.A6_YTPINTB='1'
           AND SA1.A1_COD BETWEEN %exp:cA1CODDe% AND %exp:cA1CODAte%
           AND SE1.E1_VENCTO BETWEEN %exp:cE1VENCTODe% AND %exp:cE1VENCTOAte%
           AND SE1.E1_DATABOR BETWEEN %exp:cE1DATABORDe% AND %exp:cE1DATABORAte%
           AND SE1.E1_EMISSAO BETWEEN %exp:cE1EMISSAODe% AND %exp:cE1EMISSAOAte%
         ORDER BY SE1.E1_FILIAL
                ,SE1.E1_DATABOR
                ,SA1.A1_FILIAL
                ,SA1.A1_COD
                ,SE1.E1_PREFIXO
                ,SE1.E1_NUM
                ,SE1.E1_PARCELA
    endSQL    

    cSQLQuery:=getLastQuery()[2]
    cSQLQuery:=strTran(cSQLQuery,"(1=2)","(1=1)")
    
    A35():writeSQLFile(@cSQLQuery,&("cEmpAnt"),&("cFilAnt"),"FIDCRelatorioRemessa","01","qry_final",@cSQLPath,@cSQLFile)

    aFields:=(cTmpAlias)->(dbStruct())
    nFields:=len(aFields)

    (cTmpAlias)->(dbCloseArea())

    oFWTTable:=FWTemporaryTable():New(cTmpAlias,aFields)
    oFWTTable:Create()

	aAdd(aFWTTables,oFWTTable)

    cTmpTable:=oFWTTable:getRealName()
    
    cSQLInsert:="INSERT INTO"
    cSQLInsert+=" "
    cSQLInsert+=cTmpTable
    cSQLInsert+=" "
    cSQLInsert+="("
    for nField:=1 to nFields
        cField:=aFields[nField][DBS_NAME]
		cSQLInsert+=cField
        cSQLInsert+=","
    next nField
    cSQLInsert:=subStr(cSQLInsert,1,(Len(cSQLInsert)-1))
    cSQLInsert+=")"
    cSQLInsert+=" "
    cSQLInsert+=cSQLQuery

    begin transaction
        TCSQLExec(cSQLInsert)
    end transaction

    cAPITable:=StrTran(cAPITable,"%","")
    A35():tmpTableDrop(cAPITable)

    (cTmpAlias)->(dbGoTop())

	return(cTmpAlias)

static method getQueryRet(aParamRet,aBCOIsFIDC,aFWTTables) class FIDC

    local aFields       as array

    local cA6COD        as character
    local cA6AGENCIA    as character
    local cA6NUMCON     as character

    local cSQLPath      as character
    local cSQLFile      as character
    local cSQLQuery     as character
    local cSQLInsert    as character

    local cTmpAlias     as character
    local cTmpTable     as character

    local cA1CODDe      as character
    local cA1CODAte     as character

    local cE1DATABORDe  as character
    local cE1DATABORAte as character

    local cE1EMISSAODe  as character
    local cE1EMISSAOAte as character

    local cE1VENCTODe   as character
    local cE1VENCTOAte  as character

    local cZK4DataDe    as character
    local cZK4DataAte   as character

    local cField        as character
	local cSQLFields	as character

    local nField        as numeric
    local nFields       as numeric

	local oFWTTable		as object

	paramtype aParamRet as array
	paramtype aBCOIsFIDC as array

	paramtype aFWTTables as array optional default array(0)

    cA6COD:=aBCOIsFIDC[3]
    cA6AGENCIA:=aBCOIsFIDC[4]
    cA6NUMCON:=aBCOIsFIDC[5]

    cA1CODDe:=aParamRet[1]
    cA1CODAte:=aParamRet[2]

    cE1DATABORDe:=DToS(aParamRet[3])
    cE1DATABORAte:=DToS(aParamRet[4])

    cE1EMISSAODe:=DToS(aParamRet[5])
    cE1EMISSAOAte:=DToS(aParamRet[6])

    cE1VENCTODe:=DToS(aParamRet[7])
    cE1VENCTOAte:=DToS(aParamRet[8])

    cZK4DataDe:=DToS(aParamRet[9])
    cZK4DataAte:=DToS(aParamRet[10])

	cSQLFields:=""
	cSQLFields+="SA1.A1_COD"
    cSQLFields+=",SA1.A1_NOME"
    cSQLFields+=",SE1.E1_DATABOR"
    cSQLFields+=",SE1.E1_PREFIXO+SE1.E1_NUM+SE1.E1_PARCELA AS P_NUM_PARC"
    cSQLFields+=",SE1.E1_EMISSAO"
    cSQLFields+=",SE1.E1_VENCTO"
    cSQLFields+=",0  AS DIAS_ANTEC"
    cSQLFields+=",'' AS E1_VENCREA"
    cSQLFields+=",SE1.E1_VALOR"
    cSQLFields+=",SE1.E1_YFDCVAL"
    cSQLFields+=",LTRIM(RTRIM(ZK4.ZK4_NOSNUM)) AS ZK4_NOSNUM"

	if (stacktools():IsInCallStack("getQueryCon"))
		cSQLFields+=",SE1.R_E_C_N_O_ AS SE1RECNO"
	endif

	cSQLFields:="%"+cSQLFields+"%"

    cTmpAlias:=getNextAlias()
    beginSQL alias cTmpAlias

        %noparser%

        COLUMN E1_DATABOR  AS DATE
        COLUMN E1_EMISSAO  AS DATE
        COLUMN E1_VENCTO   AS DATE
        COLUMN E1_VENCREA  AS DATE
        COLUMN ZK4_DATA    AS DATE

        SELECT %exp:cSQLFields%
         FROM %table:SE1% SE1 (NOLOCK)
         JOIN %table:ZK4% ZK4 (NOLOCK)
           ON (
                    ZK4.%notDel%
                AND ZK4.ZK4_FILIAL=%xFilial:ZK4%
                AND SE1.E1_PORTADO=ZK4.ZK4_BANCO
                AND SE1.E1_AGEDEP=ZK4.ZK4_AGENCI
                AND SE1.E1_CONTA=ZK4.ZK4_CONTA
                AND SE1.E1_NUM+SE1.E1_PARCELA=ZK4.ZK4_NUMERO
                AND SE1.E1_NUMBCO=SUBSTRING(ZK4.ZK4_NOSNUM,1,11)
                AND SE1.E1_VENCTO=ZK4.ZK4_DTVENC
                AND ZK4.ZK4_CODOCO='02'
                AND ZK4.ZK4_TIPO='R'
                AND ZK4.ZK4_DATA BETWEEN %exp:cZK4DataDe% AND %exp:cZK4DataAte%
                AND ZK4.ZK4_EMP=%exp:cEmpAnt%
                AND ZK4.ZK4_FIL=%exp:cFilAnt%
                AND ZK4.ZK4_STATUS='2'
            )
         JOIN %table:SA1% SA1 (NOLOCK) ON (
                SA1.%notDel%
            AND SA1.A1_FILIAL=%xFilial:SA1%
            AND SE1.E1_CLIENTE=SA1.A1_COD
            AND SE1.E1_LOJA=SA1.A1_LOJA
         )
         JOIN %table:SA6% SA6 (NOLOCK) ON (
                SA6.%notDel%  
            AND SA6.A6_FILIAL=%xFilial:SA6%
            AND SA6.A6_COD=%exp:cA6COD%
            AND SA6.A6_AGENCIA=%exp:cA6AGENCIA%
            AND SA6.A6_NUMCON=%exp:cA6NUMCON%
            AND SE1.E1_PORTADO=SA6.A6_COD
            AND SE1.E1_AGEDEP=SA6.A6_AGENCIA
            AND SE1.E1_CONTA=SA6.A6_NUMCON
            AND SA6.A6_YTPINTB='1'
         )
        WHERE (1=2)
            AND SE1.%notDel%
            AND SA1.%notDel%
            AND SA6.%notDel%
            AND ZK4.%notDel%
            AND SE1.E1_FILIAL=%xFilial:SE1%
            AND SA1.A1_FILIAL=%xFilial:SA1%
            AND SA6.A6_FILIAL=%xFilial:SA6%
            AND SE1.E1_CLIENTE=SA1.A1_COD
            AND SE1.E1_LOJA=SA1.A1_LOJA
            AND SA6.A6_COD=%exp:cA6COD%
            AND SA6.A6_AGENCIA=%exp:cA6AGENCIA%
            AND SA6.A6_NUMCON=%exp:cA6NUMCON%
            AND SE1.E1_PORTADO=SA6.A6_COD
            AND SE1.E1_AGEDEP=SA6.A6_AGENCIA
            AND SE1.E1_CONTA=SA6.A6_NUMCON
            AND SA6.A6_YTPINTB='1'
            AND SA1.A1_COD BETWEEN %exp:cA1CODDe% AND %exp:cA1CODAte%
            AND SE1.E1_VENCTO BETWEEN %exp:cE1VENCTODe% AND %exp:cE1VENCTOAte%
            AND SE1.E1_DATABOR BETWEEN %exp:cE1DATABORDe% AND %exp:cE1DATABORAte%
            AND SE1.E1_EMISSAO BETWEEN %exp:cE1EMISSAODe% AND %exp:cE1EMISSAOAte%
            AND ZK4.ZK4_DATA BETWEEN %exp:cZK4DataDe% AND %exp:cZK4DataAte%
            AND ZK4.ZK4_CODOCO='02'
            AND ZK4.ZK4_TIPO='R'
            AND ZK4.ZK4_STATUS='2'
        ORDER BY SE1.E1_FILIAL
                ,SE1.E1_DATABOR
                ,SA1.A1_FILIAL
                ,SA1.A1_COD
                ,SE1.E1_PREFIXO
                ,SE1.E1_NUM
                ,SE1.E1_PARCELA

    endSQL    

    if (IsBlind())
        cSQLPath:="\tmp\"
    else
        cSQLPath:=getTempPath()
        if (!right(cSQLPath,1)=="\")
            cSQLPath+="\"
        endif
    endif
    cSQLPath+="FIDC\SQL\"

    cSQLQuery:=getLastQuery()[2]
    cSQLQuery:=strTran(cSQLQuery,"(1=2)","(1=1)")
    
    A35():writeSQLFile(@cSQLQuery,&("cEmpAnt"),&("cFilAnt"),"FIDCRelatorioRetorno","00","qry_final",@cSQLPath,@cSQLFile)

    aFields:=(cTmpAlias)->(dbStruct())
    nFields:=len(aFields)

    (cTmpAlias)->(dbCloseArea())

    oFWTTable:=FWTemporaryTable():New(cTmpAlias,aFields)
    oFWTTable:Create()

	aAdd(aFWTTables,oFWTTable)

    cTmpTable:=oFWTTable:getRealName()
    
    cSQLInsert:="INSERT INTO"
    cSQLInsert+=" "
    cSQLInsert+=cTmpTable
    cSQLInsert+=" "
    cSQLInsert+="("
    for nField:=1 to nFields
        cField:=aFields[nField][DBS_NAME]
        cSQLInsert+=cField
        cSQLInsert+=","
    next nField
    cSQLInsert:=subStr(cSQLInsert,1,(Len(cSQLInsert)-1))
    cSQLInsert+=")"
    cSQLInsert+=" "
    cSQLInsert+=cSQLQuery

    begin transaction
        TCSQLExec(cSQLInsert)
    end transaction

    (cTmpAlias)->(dbGoTop())

    return(cTmpAlias)

static method getQueryRec(aParamRet,aBCOIsFIDC,aFWTTables) class FIDC

    local aFields       as array

    local cA6COD        as character
    local cA6AGENCIA    as character
    local cA6NUMCON     as character

    local cSQLPath      as character
    local cSQLFile      as character
    local cSQLQuery     as character
    local cSQLInsert    as character

    local cTmpAlias     as character
    local cTmpTable     as character

    local cA1CODDe      as character
    local cA1CODAte     as character

    local cE1DATABORDe  as character
    local cE1DATABORAte as character

    local cE1EMISSAODe  as character
    local cE1EMISSAOAte as character

    local cE1VENCTODe   as character
    local cE1VENCTOAte  as character

    local cZK4DataDe    as character
    local cZK4DataAte   as character

    local cField        as character
	local cSQLFields 	as character	

    local nField        as numeric
    local nFields       as numeric

	local oFWTTable		as numeric

	paramtype aParamRet as array
	paramtype aBCOIsFIDC as array

	paramtype aFWTTables as array optional default array(0)

    cA6COD:=aBCOIsFIDC[3]
    cA6AGENCIA:=aBCOIsFIDC[4]
    cA6NUMCON:=aBCOIsFIDC[5]

    cA1CODDe:=aParamRet[1]
    cA1CODAte:=aParamRet[2]

    cE1DATABORDe:=DToS(aParamRet[3])
    cE1DATABORAte:=DToS(aParamRet[4])

    cE1EMISSAODe:=DToS(aParamRet[5])
    cE1EMISSAOAte:=DToS(aParamRet[6])

    cE1VENCTODe:=DToS(aParamRet[7])
    cE1VENCTOAte:=DToS(aParamRet[8])

    cZK4DataDe:=DToS(aParamRet[9])
    cZK4DataAte:=DToS(aParamRet[10])

	cSQLFields:=""

	cSQLFields+=" SA1.A1_COD"
    cSQLFields+=",SA1.A1_NOME"
    cSQLFields+=",SE1.E1_DATABOR"
    cSQLFields+=",SE1.E1_PREFIXO+SE1.E1_NUM+SE1.E1_PARCELA AS P_NUM_PARC"
    cSQLFields+=",SE1.E1_EMISSAO"
    cSQLFields+=",SE1.E1_VENCTO"
    cSQLFields+=",0  AS DIAS_ANTEC"
    cSQLFields+=",'' AS E1_VENCREA"
    cSQLFields+=",SE1.E1_VALOR"
    cSQLFields+=",SE1.E1_YFDCVAL"
    cSQLFields+=",LTRIM(RTRIM(ZK4.ZK4_NOSNUM)) AS ZK4_NOSNUM"
    cSQLFields+=",ZK4.ZK4_VLREC"
    cSQLFields+=",ZK4.ZK4_VLJURO"
    cSQLFields+=",ZK4.ZK4_DTLIQ"
    cSQLFields+=",ZK4.ZK4_DTCRED"
	
	if (stacktools():IsInCallStack("getQueryCon"))
		cSQLFields+=",SE1.R_E_C_N_O_ AS SE1RECNO"
	endif

	cSQLFields:="%"+cSQLFields+"%"

    cTmpAlias:=getNextAlias()
    beginSQL alias cTmpAlias

        %noparser%

        COLUMN E1_DATABOR  AS DATE
        COLUMN E1_EMISSAO  AS DATE
        COLUMN E1_VENCTO   AS DATE
        COLUMN E1_VENCREA  AS DATE
        COLUMN ZK4_DATA    AS DATE
        COLUMN ZK4_DTLIQ   AS DATE
        COLUMN ZK4_DTCRED  AS DATE 

        SELECT %exp:cSQLFields%
          FROM %table:SE1% SE1 (NOLOCK)
          JOIN %table:ZK4% ZK4 (NOLOCK)
            ON (
                    ZK4.%notDel%
                AND ZK4.ZK4_FILIAL=%xFilial:ZK4%
                AND SE1.E1_PORTADO=ZK4.ZK4_BANCO
                AND SE1.E1_AGEDEP=ZK4.ZK4_AGENCI
                AND SE1.E1_CONTA=ZK4.ZK4_CONTA
                AND SE1.E1_NUM+SE1.E1_PARCELA=ZK4.ZK4_NUMERO
                AND SE1.E1_NUMBCO=SUBSTRING(ZK4.ZK4_NOSNUM,1,11)
                AND SE1.E1_VENCTO=ZK4.ZK4_DTVENC
                AND ZK4.ZK4_CODOCO='06'
                AND ZK4.ZK4_TIPO='R'
                AND ZK4.ZK4_DATA BETWEEN %exp:cZK4DataDe% AND %exp:cZK4DataAte%
                AND ZK4.ZK4_EMP=%exp:cEmpAnt%
                AND ZK4.ZK4_FIL=%exp:cFilAnt%
                AND ZK4.ZK4_STATUS='2'
            )
         JOIN %table:SA1% SA1 (NOLOCK) ON (
                SA1.%notDel%
            AND SA1.A1_FILIAL=%xFilial:SA1%
            AND SE1.E1_CLIENTE=SA1.A1_COD
            AND SE1.E1_LOJA=SA1.A1_LOJA
         )
         JOIN %table:SA6% SA6 (NOLOCK) ON (
                SA6.%notDel%  
            AND SA6.A6_FILIAL=%xFilial:SA6%
            AND SA6.A6_COD=%exp:cA6COD%
            AND SA6.A6_AGENCIA=%exp:cA6AGENCIA%
            AND SA6.A6_NUMCON=%exp:cA6NUMCON%
            AND SE1.E1_PORTADO=SA6.A6_COD
            AND SE1.E1_AGEDEP=SA6.A6_AGENCIA
            AND SE1.E1_CONTA=SA6.A6_NUMCON
            AND SA6.A6_YTPINTB='1'
         )
        WHERE (1=2)
            AND SE1.%notDel%
            AND SA1.%notDel%
            AND SA6.%notDel%
            AND ZK4.%notDel%
            AND SE1.E1_FILIAL=%xFilial:SE1%
            AND SA1.A1_FILIAL=%xFilial:SA1%
            AND SA6.A6_FILIAL=%xFilial:SA6%
            AND SE1.E1_CLIENTE=SA1.A1_COD
            AND SE1.E1_LOJA=SA1.A1_LOJA
            AND SA6.A6_COD=%exp:cA6COD%
            AND SA6.A6_AGENCIA=%exp:cA6AGENCIA%
            AND SA6.A6_NUMCON=%exp:cA6NUMCON%
            AND SE1.E1_PORTADO=SA6.A6_COD
            AND SE1.E1_AGEDEP=SA6.A6_AGENCIA
            AND SE1.E1_CONTA=SA6.A6_NUMCON
            AND SA6.A6_YTPINTB='1'
            AND SA1.A1_COD BETWEEN %exp:cA1CODDe% AND %exp:cA1CODAte%
            AND SE1.E1_VENCTO BETWEEN %exp:cE1VENCTODe% AND %exp:cE1VENCTOAte%
            AND SE1.E1_DATABOR BETWEEN %exp:cE1DATABORDe% AND %exp:cE1DATABORAte%
            AND SE1.E1_EMISSAO BETWEEN %exp:cE1EMISSAODe% AND %exp:cE1EMISSAOAte%
            AND ZK4.ZK4_DATA BETWEEN %exp:cZK4DataDe% AND %exp:cZK4DataAte%
            AND ZK4.ZK4_CODOCO='06'
            AND ZK4.ZK4_TIPO='R'
            AND ZK4.ZK4_STATUS='2'
        ORDER BY SE1.E1_FILIAL
                ,SE1.E1_DATABOR
                ,SA1.A1_FILIAL
                ,SA1.A1_COD
                ,SE1.E1_PREFIXO
                ,SE1.E1_NUM
                ,SE1.E1_PARCELA

    endSQL    

    if (IsBlind())
        cSQLPath:="\tmp\"
    else
        cSQLPath:=getTempPath()
        if (!right(cSQLPath,1)=="\")
            cSQLPath+="\"
        endif
    endif
    cSQLPath+="FIDC\SQL\"

    cSQLQuery:=getLastQuery()[2]
    cSQLQuery:=strTran(cSQLQuery,"(1=2)","(1=1)")
    
    A35():writeSQLFile(@cSQLQuery,&("cEmpAnt"),&("cFilAnt"),"FIDCRelatorioRecebidos","00","qry_final",@cSQLPath,@cSQLFile)

    aFields:=(cTmpAlias)->(dbStruct())
    nFields:=len(aFields)

    (cTmpAlias)->(dbCloseArea())

    oFWTTable:=FWTemporaryTable():New(cTmpAlias,aFields)
    oFWTTable:Create()

	aAdd(aFWTTables,oFWTTable)
	
    cTmpTable:=oFWTTable:getRealName()
    
    cSQLInsert:="INSERT INTO"
    cSQLInsert+=" "
    cSQLInsert+=cTmpTable
    cSQLInsert+=" "
    cSQLInsert+="("
    for nField:=1 to nFields
        cField:=aFields[nField][DBS_NAME]
        cSQLInsert+=cField
        cSQLInsert+=","
    next nField
    cSQLInsert:=subStr(cSQLInsert,1,(Len(cSQLInsert)-1))
    cSQLInsert+=")"
    cSQLInsert+=" "
    cSQLInsert+=cSQLQuery

    begin transaction
        TCSQLExec(cSQLInsert)
    end transaction

    (cTmpAlias)->(dbGoTop())

    return(cTmpAlias)

static method getQueryCon(aParamRet,aBCOIsFIDC,aFWTTables) class FIDC

    local aFields       as array

    local cA6COD        as character
    local cA6AGENCIA    as character
    local cA6NUMCON     as character

    local cSQLPath      as character
    local cSQLFile      as character
    local cSQLQuery     as character
    local cSQLInsert    as character

    local cTmpAlias     as character
    local cTmpTable     as character

    local cA1CODDe      as character
    local cA1CODAte     as character

    local cE1DATABORDe  as character
    local cE1DATABORAte as character

    local cE1EMISSAODe  as character
    local cE1EMISSAOAte as character

    local cE1VENCTODe   as character
    local cE1VENCTOAte  as character

    local cZK4DataDe    as character
    local cZK4DataAte   as character

    local cField        as character

	local cTableRem		as character
	local cTableRet		as character
	local cTableRec		as character

    local nField        as numeric
    local nFields       as numeric

	local oFWTTable		as object

	paramtype aParamRet as array
	paramtype aBCOIsFIDC as array

	paramtype aFWTTables as array optional DEFAULT array(0)

    cA6COD:=aBCOIsFIDC[3]
    cA6AGENCIA:=aBCOIsFIDC[4]
    cA6NUMCON:=aBCOIsFIDC[5]

    cA1CODDe:=aParamRet[1]
    cA1CODAte:=aParamRet[2]

    cE1DATABORDe:=DToS(aParamRet[3])
    cE1DATABORAte:=DToS(aParamRet[4])

    cE1EMISSAODe:=DToS(aParamRet[5])
    cE1EMISSAOAte:=DToS(aParamRet[6])

    cE1VENCTODe:=DToS(aParamRet[7])
    cE1VENCTOAte:=DToS(aParamRet[8])

    cZK4DataDe:=DToS(aParamRet[9])
    cZK4DataAte:=DToS(aParamRet[10])

	cTmpAlias:=FIDC():getQueryRem(@aParamRet,@aBCOIsFIDC,@aFWTTables)
	if (select(cTmpAlias)>0)
		cTableRem:=aFWTTables[len(aFWTTables)]:getRealName()
		cTableRem:="%"+cTableRem+"%"
		(cTmpAlias)->(dbCloseArea())
	endif
	
	cTmpAlias:=FIDC():getQueryRet(@aParamRet,@aBCOIsFIDC,@aFWTTables)
	if (select(cTmpAlias)>0)
		cTableRet:=aFWTTables[len(aFWTTables)]:getRealName()
		cTableRet:="%"+cTableRet+"%"
		(cTmpAlias)->(dbCloseArea())
	endif

	cTmpAlias:=FIDC():getQueryRec(@aParamRet,@aBCOIsFIDC,@aFWTTables)
	if (select(cTmpAlias)>0)
		cTableRec:=aFWTTables[len(aFWTTables)]:getRealName()
		cTableRec:="%"+cTableRec+"%"
		(cTmpAlias)->(dbCloseArea())
	endif

    cTmpAlias:=getNextAlias()
    beginSQL alias cTmpAlias

        %noparser%

        COLUMN E1_DATABOR  AS DATE
        COLUMN E1_EMISSAO  AS DATE
        COLUMN E1_VENCTO   AS DATE

        SELECT REM.A1_COD
              ,REM.A1_NOME
              ,REM.E1_DATABOR
              ,REM.P_NUM_PARC
              ,REM.E1_EMISSAO
              ,REM.E1_VENCTO
			  ,ISNULL(REM.E1_VALOR,0)  AS ENVIADO
			  ,ISNULL(RET.E1_VALOR,0)  AS REGISTRADO
			  ,ISNULL(REC.ZK4_VLREC,0) AS BAIXADO
			  ,(CASE WHEN ISNULL(REM.E1_VALOR,0)=ISNULL(RET.E1_VALOR,0) THEN 'CONCILIADO' ELSE 'PENDENTE' END) AS REGISTRO
			  ,(CASE WHEN ISNULL(REC.ZK4_VLREC,0)>=ISNULL(REM.E1_VALOR,0) THEN 'BAIXADO' ELSE 'PENDENTE' END)  AS BAIXA
        FROM %exp:cTableRem% REM (NOLOCK)
        FULL OUTER JOIN %exp:cTableRet% RET (NOLOCK)
            ON (
                    REM.SE1RECNO=RET.SE1RECNO
         )
        FULL OUTER JOIN %exp:cTableRec% REC (NOLOCK) ON (
                REM.SE1RECNO=REC.SE1RECNO
         )
        WHERE (1=2)
        ORDER BY REM.E1_DATABOR
                ,REM.A1_COD
                ,REM.P_NUM_PARC

    endSQL    

    if (IsBlind())
        cSQLPath:="\tmp\"
    else
        cSQLPath:=getTempPath()
        if (!right(cSQLPath,1)=="\")
            cSQLPath+="\"
        endif
    endif
    cSQLPath+="FIDC\SQL\"

    cSQLQuery:=getLastQuery()[2]
    cSQLQuery:=strTran(cSQLQuery,"(1=2)","(1=1)")
    
    A35():writeSQLFile(@cSQLQuery,&("cEmpAnt"),&("cFilAnt"),"FIDCRelatorioRecebidos","00","qry_final",@cSQLPath,@cSQLFile)

    aFields:=(cTmpAlias)->(dbStruct())
    nFields:=len(aFields)

    (cTmpAlias)->(dbCloseArea())

    oFWTTable:=FWTemporaryTable():New(cTmpAlias,aFields)
    oFWTTable:Create()
	
    aAdd(aFWTTables,oFWTTable)

	cTmpTable:=oFWTTable:getRealName()

    cSQLInsert:="INSERT INTO"
    cSQLInsert+=" "
    cSQLInsert+=cTmpTable
    cSQLInsert+=" "
    cSQLInsert+="("
    for nField:=1 to nFields
        cField:=aFields[nField][DBS_NAME]
        cSQLInsert+=cField
        cSQLInsert+=","
    next nField
    cSQLInsert:=subStr(cSQLInsert,1,(Len(cSQLInsert)-1))
    cSQLInsert+=")"
    cSQLInsert+=" "
    cSQLInsert+=cSQLQuery

    begin transaction
        TCSQLExec(cSQLInsert)
    end transaction

    (cTmpAlias)->(dbGoTop())

	return(cTmpAlias)
